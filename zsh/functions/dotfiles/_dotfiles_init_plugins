#!/bin/zsh

function _dotfiles_init_plugins() {
  echo "[dotfiles] initialising plugins"

  # clear init.zsh
  if [[ -e "$DOTFILES_HOME/zsh/plugins/init.zsh" ]]; then
    truncate -s 0 "$DOTFILES_HOME/zsh/plugins/init.zsh"
  else
    touch "$DOTFILES_HOME/zsh/plugins/init.zsh"
  fi

  for plugin in $dotfiles_plugins; do
    # find zsh file for plugin
    init_candidates=($DOTFILES_HOME/zsh/plugins/$plugin/*{.plugin,}.{zsh-theme,zsh,sh}(N))
    [[ -n ${init_candidates} ]] || { echo >&2 "[dotfiles] no init file found for $plugin!" && continue }

    # in init.zsh: source plugin
    echo "source ${init_candidates[1]}" >> "$DOTFILES_HOME/zsh/plugins/init.zsh"

    # compile plugin files
    echo "[dotfiles] compiling $plugin"
    for plugin_file in $DOTFILES_HOME/zsh/plugins/**/*.{sh,zsh,zsh-theme}; do
      _dotfiles_compile_file $plugin_file
    done
  done

  # compile init file
  _dotfiles_compile_file "$DOTFILES_HOME/zsh/plugins/init.zsh"

  echo "[dotfiles] plugins initialised"
}
