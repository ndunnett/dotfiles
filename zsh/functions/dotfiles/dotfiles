#!/bin/zsh

function dotfiles() {
  while [ $# -gt 0 ]; do
    case "$1" in
      (benchmark)
        if [[ ! -d "$DOTFILES_HOME/zsh/plugins/zsh-bench" ]]; then
          echo "[dotfiles] zsh-bench not found, cloning repo"
          git clone -q --depth 1 --recursive --shallow-submodules "https://github.com/romkatv/zsh-bench.git" "$DOTFILES_HOME/zsh/plugins/zsh-bench"
        elif ! git -C "$DOTFILES_HOME/zsh/plugins/zsh-bench" diff-index --quiet HEAD; then
          echo "[dotfiles] zsh-bench out of date, pulling repo"
          git -C "$DOTFILES_HOME/zsh/plugins/zsh-bench" pull -q
        fi
        zsh "$DOTFILES_HOME/zsh/plugins/zsh-bench/zsh-bench"
      ;;

      (reload)
        echo "[dotfiles] reloading shell"
        exec zsh -l
      ;;

      (flush)
        echo "[dotfiles] flushing downloaded/generated files"
        rm -rf $DOTFILES_HOME/zsh/plugins
        rm -f $DOTFILES_HOME/zsh/functions/init.zsh
        rm -f $DOTFILES_HOME/zsh/**/*.zwc
        dotfiles reload
      ;;

      (recompile)
        echo "[dotfiles] removing compiled files"
        rm -f $DOTFILES_HOME/zsh/plugins/init.zsh
        rm -f $DOTFILES_HOME/zsh/functions/init.zsh
        rm -f $DOTFILES_HOME/zsh/**/*.zwc
        _dotfiles_init_functions
        _dotfiles_init_plugins
        dotfiles reload
      ;;

      (load_plugins)
        [[ -v dotfiles_plugins ]] || echo >&2 "[dotfiles] plugins not defined!"
        [[ -d "$DOTFILES_HOME/zsh/plugins" ]] || dotfiles update
        [[ -e "$DOTFILES_HOME/zsh/plugins/init.zsh" ]] || _dotfiles_init_plugins
        source "$DOTFILES_HOME/zsh/plugins/init.zsh"
      ;;

      (update)
        _dotfiles_update
      ;;

      (help)
        echo "[dotfiles] valid usage: dotfiles [ reload | load_plugins | update | flush | recompile | benchmark ]"
        echo "[dotfiles] work in progress, look at source in config/zsh/functions/dotfiles/dotfiles"
      ;;

      (*)
        echo "[dotfiles] bad argument: $1"
      ;;
    esac
    shift
  done
}
