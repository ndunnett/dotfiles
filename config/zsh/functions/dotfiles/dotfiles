#!/bin/zsh

function dotfiles() {
  while [ $# -gt 0 ]; do
    case "$1" in
      (benchmark)
        if [[ ! -d "$ZSH_HOME/plugins/zsh-bench" ]]; then
          echo "[dotfiles] zsh-bench not found, cloning repo"
          git clone -q --depth 1 --recursive --shallow-submodules "https://github.com/romkatv/zsh-bench.git" "$ZSH_HOME/plugins/zsh-bench"
        elif ! git -C "$ZSH_HOME/plugins/zsh-bench" diff-index --quiet HEAD; then
          echo "[dotfiles] zsh-bench out of date, pulling repo"
          git -C "$ZSH_HOME/plugins/zsh-bench" pull -q
        fi
        zsh "$ZSH_HOME/plugins/zsh-bench/zsh-bench"
      ;;

      (reload)
        echo "[dotfiles] reloading shell"
        exec zsh -l
      ;;

      (flush)
        echo "[dotfiles] flushing downloaded/generated files"
        rm -rf $ZSH_HOME/plugins
        rm -f $ZSH_HOME/functions/init.zsh
        rm -f $ZSH_HOME/**/*.zwc
        dotfiles reload
      ;;

      (recompile)
        echo "[dotfiles] removing compiled files"
        rm -f $ZSH_HOME/plugins/init.zsh
        rm -f $ZSH_HOME/functions/init.zsh
        rm -f $ZSH_HOME/**/*.zwc
        _dotfiles_init_functions
        _dotfiles_init_plugins
        dotfiles reload
      ;;

      (load_plugins)
        [[ -v dotfiles_plugins ]] || echo >&2 "[dotfiles] plugins not defined!"
        [[ -d "$ZSH_HOME/plugins" ]] || dotfiles update
        [[ -e "$ZSH_HOME/plugins/init.zsh" ]] || _dotfiles_init_plugins
        source "$ZSH_HOME/plugins/init.zsh"
      ;;

      (update)
        _dotfiles_update
      ;;

      (help)
        echo "[dotfiles] valid usage: dotfiles [ reload | load_plugins | update | flush | recompile | benchmark ]"
        echo "[dotfiles] work in progress, look at source in config/zsh/functions/dotfiles/dotfiles"
      ;;

      (*)
        echo "[dotfiles] bad argument: $1"
      ;;
    esac
    shift
  done
}
